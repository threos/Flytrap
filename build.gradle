import edu.wpi.first.gradlerio.GradleRIOPlugin

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id "edu.wpi.first.GradleRIO" version "2022.3.1"
    id 'application'
}

group = 'com.teambutterflyeffect'
version = '1.0-SNAPSHOT'

def ROBOT_MAIN_CLASS = "com.teambutterflyeffect.flytrap.entrypoint.RobotLauncherKt"

repositories {
    mavenCentral()
    maven {
        url "https://devsite.ctr-electronics.com/maven/release/"
    }
}

// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        roborio(getTargetTypeClass('RoboRIO')) {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = project.frc.getTeamNumber()
            debug = project.frc.getDebugOrDefault(false)

            artifacts {
                // First part is artifact name, 2nd is artifact type
                // getTargetTypeClass is a shortcut to get the class type using a string

                frcJava(getArtifactTypeClass('FRCJavaArtifact')) {
                }

                // Static files artifact
                frcStaticFileDeploy(getArtifactTypeClass('FileTreeArtifact')) {
                    files = project.fileTree('src/main/deploy')
                    directory = '/home/lvuser/deploy'
                }
            }
        }
    }
}


dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    //implementation 'edu.wpi.first.wpilibj:wpilibj-java:2022.3.1'

    implementation 'io.ktor:ktor-server-core:1.6.7'
    implementation 'io.ktor:ktor-server-netty:1.6.7'
    implementation 'io.ktor:ktor-jackson:1.6.7'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.1'

    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    roborioDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.roborio)
    roborioDebug wpi.java.vendor.jniDebug(wpi.platforms.roborio)

    roborioRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.roborio)
    roborioRelease wpi.java.vendor.jniRelease(wpi.platforms.roborio)

    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
    simulationDebug wpi.sim.enableDebug()

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
    simulationRelease wpi.sim.enableRelease()
}

def deployArtifact = deploy.targets.roborio.artifacts.frcJava

wpi.java.debugJni = false


jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

deployArtifact.jarTask = jar
wpi.java.configureExecutableTasks(jar)
wpi.java.configureTestTasks(test)

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '11'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '11'
}

application {
    mainClassName = 'MainKt'
}